# Cormack-Jolly-Seber model - p.179 on Kery and 
# Schaub

model {
  for (i in 1:nind){
    phi[i] <- mean.phi
  }

  for (t in 1:T){
      logit(p[t]) <- mu + beta * x[t] + epsilon[t]
      epsilon[t] ~ dnorm(0, 1/var_logitP)
      #p[t] ~ dbeta(5, 2)  #<- mean.p
      N[t] <- n[t]/p[t]
  }

  mean.phi ~ dbeta(5, 2) #dunif(0,1)

  mean.p ~ dbeta(5, 2) #unif(0,1)
  mu <- log(mean.p / (1 - mean.p))
  beta ~ dnorm(0, 0.01)

  var_logitP ~ dgamma(5, 0.1)
  sigma_logitP <- sqrt(var_logitP)
  
  # Likelihood:
  for (i in 1:nind){
    # define latent state at first capture
    z[i, f[i]] <- 1
    for (t in (f[i] + 1):n.occasions){
      # state process
      z[i,t] ~ dbern(mu1[i,t])
      mu1[i,t] <- (phi[i] ^ dt[t]) * z[i, t-1]

      # observation process
      y[i,t] ~ dbern(mu2[i,t])
      mu2[i,t] <- p[t-1] * z[i,t]
      
      loglik[i,t] <- logdensity.bern(y[i,t], mu2[i,t])
    }
  }

}


