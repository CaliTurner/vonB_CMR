# Cormack-Jolly-Seber model - p.179 on Kery and 
# Schaub, with an individual-specific covariate on survival
# and fixed capture probability

model {
  for (i in 1:nind){
    logit(phi[i]) <- mu + beta * x[i] + epsilon[i]
  #}
  #
  #for (i in 1:nind){
    epsilon[i] ~ dnorm(0, tau)
  }

  mean.phi ~ dbeta(5, 2) #unif(0,1)
  mu <- log(mean.phi / (1 - mean.phi))
  beta ~ dnorm(0, 0.01)
  sigma ~ dunif(0, 3)
  tau <- pow(sigma, -2)
  #sigma2 <- pow(sigma, 2)
  mean.p ~ dbeta(5, 2)  #dunif(0,1)

  # Likelihood:
  for (i in 1:nind){
    # define latent state at first capture
    z[i, f[i]] <- 1
    for (t in (f[i] + 1):n.occasions){
      # state process
      z[i,t] ~ dbern(mu1[i,t])
      mu1[i,t] <- (phi[i] ^ dt[t]) * z[i, t-1]

      # observation process
      y[i,t] ~ dbern(mu2[i,t])
      mu2[i,t] <- mean.p * z[i,t]  
      loglik[i,t] <- logdensity.bern(y[i,t], mu2[i,t])
    }
  }

}


